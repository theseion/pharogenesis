printOn: aStream format: formatArray 
	"Print a description of the receiver on aStream using the format denoted the argument, formatArray:
		#(item item item sep monthfmt yearfmt twoDigits)
		items:  1=day  2=month  3=year  will appear in the order given,
		separated by sep which is eaither an ascii code or character.
		monthFmt:  1=09  2=Sep  3=September
		yearFmt:  1=1996  2=96
		digits:  (missing or)1=9  2=09.
	See the examples in printOn: and mmddyy"

	| gregorian twoDigits element monthFormat  |
	gregorian _ self asGregorian.
	twoDigits _ formatArray size > 6 and: [ (formatArray at: 7) > 1 ].

	1 to: 3 do: 
	[ :i | 
		element _ formatArray at: i.
		element = 1 ifTrue:
		[ 
			twoDigits 
				ifTrue: [ aStream nextPutAll: (gregorian first asString padded: #left to: 2 with: $0) ]
				ifFalse: [ gregorian first printOn: aStream ]
		].
		element = 2 ifTrue: 
		[
			monthFormat _ formatArray at: 5.
			monthFormat = 1 ifTrue:
			[
				twoDigits 
					ifTrue: [ aStream nextPutAll: (gregorian middle asString padded: #left to: 2 with: $0) ]
					ifFalse: [ gregorian middle printOn: aStream ].
			].
			monthFormat = 2 ifTrue:
			[
				aStream nextPutAll: ((MonthNames at: gregorian middle) copyFrom: 1 to: 3)
			].
			monthFormat = 3 ifTrue:
			[
				aStream nextPutAll: (MonthNames at: gregorian middle)
			].
		].
		element = 3 ifTrue: 
		[
			(formatArray at: 6) = 1
				ifTrue: [ gregorian last printOn: aStream ]
				ifFalse: [ aStream nextPutAll: ((gregorian last \\ 100) asString padded: #left to: 2 with: $0) ].
		].
		i < 3 ifTrue: 
		[
			(formatArray at: 4) ~= 0 
				ifTrue: [ aStream nextPut: (formatArray at: 4) asCharacter ] 
		].
	].